pragma ever-solidity >= 0.64.0;

import "broxus-ton-tokens-contracts/contracts/interfaces/ITokenRoot.tsol";
import "broxus-ton-tokens-contracts/contracts/interfaces/ITokenWallet.tsol";
import "broxus-ton-tokens-contracts/contracts/interfaces/IAcceptTokensTransferCallback.tsol";

import '@itgold/everscale-tip/contracts/TIP4_3/TIP4_3Collection.sol';
import '@itgold/everscale-tip/contracts/TIP4_2/TIP4_2Collection.sol';

import "./structures/ITile.tsol";
import "./interfaces/ISegmintCollection.tsol";

import "./libraries/MsgFlag.tsol";
import "./libraries/ErrorCodes.tsol";
import './SegmintNft.tsol';

contract SegmintCollection is TIP4_2Collection, TIP4_3Collection, ITile, ISegmintCollection, IAcceptTokensTransferCallback {
    uint16 private static nonce_;

    address private static owner_;
    address private newOwner_;

    address private tokenRoot_;
    address private tokenWallet_;
    uint128 private onePixelTokenPrice_;

    bool private mintDisabled_;

    // id of nft minted
    uint32 private nftCounter_;

    // x = 0..100, y = 0.100 (100 pieces by 10 pixes - 1000x1000)
    // uint7 - 128 - x coord << 7
    // uint7 - 128 - y coord
    mapping (uint14 => Tile) tiles_;

    // Small mapping index -> nftId;
    mapping (uint14 => uint32) tileOwnerNftId_;

    uint128 remainOnNft_ = 0.3 ever;

    event MintDisabledChanged(bool newValue);
    event TileColorify(uint32 nftId, uint10 pixelStartX, uint10 pixelStartY, TileColors tileColors);
    event NftMinted(uint32 nftId, address owner, uint10 pixelStartX, uint10 pixelStartY, uint10 pixelEndX, uint10 pixelEndY);
    event OnePixelTokenPriceChanged(uint128 newPrice);

    constructor(
        TvmCell codeNft,
        TvmCell codeIndex,
        TvmCell codeIndexBasis,
        string jsonMetadata,
        address tokenRoot,
        uint128 onePixelTokenPrice
    ) TIP4_1Collection (
        codeNft
    ) TIP4_2Collection (
        jsonMetadata
    ) TIP4_3Collection (
        codeIndex,
        codeIndexBasis
    ) public {
        require(msg.value >= 3 ever, ErrorCodes.NOT_ENOUGH_GAS);
        require(msg.sender == owner_, ErrorCodes.NOT_OWNER);
        require(tvm.pubkey() == 0, ErrorCodes.PUBKEY_MUST_BE_NOT_SET);
        tvm.rawReserve(1 ever, 0);
        mintDisabled_ = true;
        tokenRoot_ = tokenRoot;
        onePixelTokenPrice_ = onePixelTokenPrice;
        ITokenRoot(tokenRoot_).deployWallet{
            value: 0,
            bounce: false,
            flag: 128,
            callback: onWalletDeployed
        }(address(this), 0.15 ever);
    }

    modifier onlyOwner() {
        require(msg.sender == owner_, ErrorCodes.NOT_OWNER);
        _;
    }

    modifier onlyTokenWallet() {
        require(tokenWallet_.value != 0 && msg.sender == tokenWallet_, ErrorCodes.INVALID_TOKEN_WALLET);
        _;
    }

    modifier onlyMintEnabled() {
        require(mintDisabled_ == false, ErrorCodes.MINT_DISABLED);
        _;
    }

    function onWalletDeployed(
        address tokenWallet
    ) public {
        require(msg.sender == tokenRoot_, ErrorCodes.INVALID_TOKEN_ROOT);
        tokenWallet_ = tokenWallet;
    }

    function changeOwner(address newOwner) external onlyOwner {
        require(newOwner.value != 0, ErrorCodes.BAD_NEW_OWNER);
        newOwner_ = newOwner;
        msg.sender.transfer(0, false, MsgFlag.REMAINING_GAS);
    }

    function acceptOwnership() external {
        require(newOwner_.value != 0, ErrorCodes.BAD_NEW_OWNER);
        require(msg.sender == newOwner_, ErrorCodes.BAD_NEW_OWNER);

        owner_ = newOwner_;
        newOwner_ = address(0);
        msg.sender.transfer(0, false, MsgFlag.REMAINING_GAS);
    }

    function disableMint() external override responsible onlyOwner returns(bool) {
        mintDisabled_ = true;
        emit MintDisabledChanged(mintDisabled_);
        return { value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false } mintDisabled_;
    }

    function enableMint() external override responsible onlyOwner returns(bool) {
        // We can't enable mint if token wallet is not deployed yet
        require(tokenWallet_.value != 0, ErrorCodes.INVALID_TOKEN_WALLET);
        mintDisabled_ = false;
        emit MintDisabledChanged(mintDisabled_);
        return { value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false } mintDisabled_;
    }

    function getMintDisabled() external override responsible returns (bool){
        return {value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false } mintDisabled_;
    }

    function withdraw(address fromWallet, address to, uint128 deploy_wallet_value, uint128 amount) external onlyOwner {
        require(to.value != 0, ErrorCodes.INVALID_RECIPIENT);
        require(msg.value > deploy_wallet_value + 0.2 ever, ErrorCodes.NOT_ENOUGH_GAS);
        // We allow to withdraw from any fromWallet
        // in case someone will send wrong tip3 token to our contract.
        tvm.rawReserve(0, 4);
        TvmCell emptyPayload;
        ITokenWallet(fromWallet).transfer{value: 0, bounce: false, flag: MsgFlag.ALL_NOT_RESERVED}(
            amount,
            to,
            deploy_wallet_value,
            msg.sender,
            false,
            emptyPayload
        );
    }

    function changeOneTilePrice(uint128 newTilePrice) external onlyOwner {
        tvm.rawReserve(0, 4);
        onePixelTokenPrice_ = newTilePrice;
        emit OnePixelTokenPriceChanged(onePixelTokenPrice_);
        msg.sender.transfer(0, false, MsgFlag.ALL_NOT_RESERVED + MsgFlag.IGNORE_ERRORS);
    }

    function _buildNftState(
        TvmCell code,
        uint256 id
    ) internal virtual override(TIP4_2Collection, TIP4_3Collection) pure returns (TvmCell) {
        return tvm.buildStateInit({
            contr: SegmintNft,
            varInit: {_id: id},
            code: code
        });
    }

    function _validateClaim(uint10 pixelStartX, uint10 pixelStartY, uint10 pixelEndX, uint10 pixelEndY, TileColors[] tilesToColorify) private inline returns (bool, uint7, uint7, uint7, uint7) {
        if (pixelStartX % 10 != 0 || pixelStartY % 10 != 0 || pixelEndX % 10 != 0 || pixelEndY % 10 != 0 || pixelStartX >= pixelEndX || pixelStartY >= pixelEndY || pixelEndX > 1000 || pixelEndY > 1000) {
            return (true, 0, 0, 0, 0);
        }

        // 0 - 100
        uint7 tilesWidthCount = uint7((pixelEndX - pixelStartX) / 10);
        uint7 tilesHeightCount = uint7((pixelEndY - pixelStartY) / 10);

        if (tilesWidthCount * tilesHeightCount != tilesToColorify.length) {
            return (true, 0, 0, 0, 0);
        }

        // 0 - 99
        uint7 tilesStartX = uint7(pixelStartX / 10);
        uint7 tilesStartY = uint7(pixelStartY / 10);

        // Check the tiles is not claimed
        bool shouldCancel = false;
        for (uint7 tileX = tilesStartX; tileX < tilesWidthCount; tileX++) {
            for (uint7 tileY = tilesStartY; tileY < tilesHeightCount; tileY++) {
                uint14 index = uint14(tileX) << 7 | uint14(tileY);
                if (tileOwnerNftId_.exists(index)) {
                    shouldCancel = true;
                }
            }
        }

        return (shouldCancel, tilesStartX, tilesStartY, tilesWidthCount, tilesHeightCount);
    }

    function onAcceptTokensTransfer(
        address tokenRoot,
        uint128 amount,
        address sender,
        address senderWallet,
        address remainingGasTo,
        TvmCell payload
    ) override external onlyTokenWallet {
        // Decode input
        (uint10 pixelStartX, uint10 pixelStartY, uint10 pixelEndX, uint10 pixelEndY, TileColors[] tilesToColorify, string description, string url, uint128 coinsToRedrawOneTile) = abi.decode(payload, (uint10, uint10, uint10, uint10, TileColors[], string, string, uint128));

        // Validate input
        (bool shouldCancel, uint7 tilesStartX, uint7 tilesStartY, uint7 tilesWidthCount, uint7 tilesHeightCount) = _validateClaim(pixelStartX, pixelStartY, pixelEndX, pixelEndY, tilesToColorify);

        // Amount must be widthTiles * heightTiles * 100 pixelPerTile * onePixelTokenPrice_
        if (mintDisabled_ || shouldCancel || msg.value < 3.5 ever || amount != uint128(tilesWidthCount) * uint128(tilesHeightCount) * uint128(100) * onePixelTokenPrice_) {
            if (msg.value < 1 ever) {
                // User didn't not attached enough value to the message
                // at this moment we are not sure we have enough
                // value (after paying for current transaction gas)
                // for safe transfer tokens back.
                // So we just throwing an error and user must
                // ask to manual refund.
                require(false, ErrorCodes.NOT_ENOUGH_GAS);
            }
            // Not enough gas or tiles already taken or not enough payment
            TvmCell emptyPayload;
            ITokenWallet(msg.sender).transferToWallet{value: 0, bounce: false, flag: 128}(amount, senderWallet, remainingGasTo, false, emptyPayload);
        } else {
            // claim!!!
            // we always store balance + 1 venom from incoming message for storage payments
            tvm.rawReserve(1 ever, 4);
            uint32 nftId = nftCounter_;
            nftCounter_ += 1;

            for (uint7 tileX = tilesStartX; tileX < tilesStartX + tilesWidthCount; tileX++) {
                uint14 shiftedX = uint14(tileX) << 7;
                for (uint7 tileY = tilesStartY; tileY < tilesStartY + tilesHeightCount; tileY++) {
                    uint14 index = shiftedX | uint14(tileY);
                    // set this tile index to be owned by nft
                    // we already checked is this index free in _validateClaim
                    tileOwnerNftId_[index] = nftId;
                }
            }

            emit NftMinted(nftId, sender, pixelStartX, pixelStartY, pixelEndX, pixelEndY);

            TvmBuilder salt;
            salt.store(address(this));
            TvmCell codeNft = tvm.setCodeSalt(_codeNft, salt.toCell());
            TvmCell stateNft = _buildNftState(codeNft, uint256(nftId));

            address nftAddr = new SegmintNft{
                    stateInit: stateNft,
                    value: 1 ever,
                    flag: 0
            } (
                sender,
                sender,
                remainOnNft_,
                _indexDeployValue,
                _indexDestroyValue,
                _codeIndex,
                pixelStartX,
                pixelStartY,
                pixelEndX,
                pixelEndY,
                description,
                url
            );
            SegmintNft(nftAddr).colorify{value: 0, bounce: false, flag: MsgFlag.ALL_NOT_RESERVED}({tilesToColorify: tilesToColorify, description: description, url: url, coinsToRedrawOneTile: coinsToRedrawOneTile, sendGasBack: sender});
        }
    }

    function colorifyTile(uint32 nftId, TileColors tileColors, uint10 tilePixelX, uint10 tilePixelY, address sendGasBack) external override {
        // we always store balance + 0.001 venom from incoming message for storage payments
        require(msg.sender == _resolveNft(nftId), ErrorCodes.WRONG_NFT);
        // we always store 0.001 venom from incomig message for storage payments
        tvm.rawReserve(0.001 ever, 4);

        uint14 index = (uint14(tilePixelX/10) << 7) | uint14(tilePixelY/10);
        optional(uint32) tileOwner = tileOwnerNftId_.fetch(index);
        if (tileOwner.hasValue()) {
            uint32 tileOwnedByNftId = tileOwner.get();
            // unnecessary check, but why not.
            require(tileOwnedByNftId == nftId, ErrorCodes.NFT_NOT_OWNER);

            // We iterate via tile color arrays
            // because i'm not really sure how solidity operate with arrays
            // and big structs. Due to TvmCell structure I think solidity doesn't
            // really check how many elements are packed into array.
            // So any bad gay can try to insert really big array to Increase contract storage
            // payments.
            uint80[] emptyColors = new uint80[](10);
            TileColors newColors = TileColors(emptyColors, emptyColors, emptyColors);
            for (uint y = 0; y < 10; y++) {
                newColors.r[y] = tileColors.r[y];
                newColors.g[y] = tileColors.g[y];
                newColors.b[y] = tileColors.b[y];
            }
            tiles_[index] = Tile(nftId, newColors);
            emit TileColorify(nftId, tilePixelX, tilePixelY, newColors);
        } else {
            require(false, ErrorCodes.NFT_NOT_OWNER);
        }
        sendGasBack.transfer( 0,  false, MsgFlag.ALL_NOT_RESERVED + MsgFlag.IGNORE_ERRORS);
    }

    // To call offchain to get tile info and pixels
    function getTile(uint10 tilePixelX, uint10 tilePixelY) external override responsible returns (uint8[] pixels, uint32 nftId) {
        // 2 ** 32
        nftId = uint32(4294967295);
        uint14 index = (uint14(tilePixelX/10) << 7) | uint14(tilePixelY/10);
        optional(Tile) oTile = tiles_.fetch(index);
        if (oTile.hasValue()) {
            Tile tile = oTile.get();
            nftId = tile.nftId;
            pixels = _convertTileToPixels(tile.colors);
        } else {
            optional(uint32) oNftId = tileOwnerNftId_.fetch(index);
            if (oNftId.hasValue()) {
                // nft claimed but not colored
                nftId = oNftId.get();
            }
            for (uint y = 0; y < 10; y++)  {
                for (uint x = 0; x < 10; x++)  {
                    pixels.push(uint8(0));
                    pixels.push(uint8(0));
                    pixels.push(uint8(0));
                }
            }
        }

        return{value: 0, bounce: false, flag: 64} (pixels, nftId);
    }

    // To call offchain to convert raw colors to pixels
    function convertTileColorsToPixels(TileColors tileColors) external override responsible returns(uint8[] pixels) {
        return{value: 0, bounce: false, flag: 64} _convertTileToPixels(tileColors);
    }

    function getTiles() external responsible returns(mapping (uint14 => Tile)) {
        return{value: 0, bounce: false, flag: 64} tiles_;
    }

    function getOnePixelTokenPrice() external responsible returns(uint128) {
        return{value: 0, bounce: false, flag: 64} onePixelTokenPrice_;
    }

    function nftCodeWithoutSalt() external view responsible returns (TvmCell nftCode) {
        return {value: 0, flag: 64, bounce: false} (_codeNft);
    }

    function getNftCount() external responsible returns(uint32) {
        return{value: 0, bounce: false, flag: 64} nftCounter_;
    }

    function getNftIndexCodeHash(
        address _owner
    ) external view responsible returns (string codeHash) {
        TvmBuilder salt;
        salt.store("nft");
        salt.store(address(this));
        salt.store(_owner);
        TvmCell code = tvm.setCodeSalt(_codeIndex, salt.toCell());
        return{value: 0, bounce: false, flag: 64} format("{:064x}", tvm.hash(code));
    }

    function _convertTileToPixels(TileColors tileColors) private inline returns (uint8[] pixels)  {
        for (uint y = 0; y < 10; y++)  {
            for (uint x = 0; x < 10; x++)  {
                pixels.push(uint8( (tileColors.r[y] >> ((9 - x) * 8)) & 0xff ));
                pixels.push(uint8( (tileColors.g[y] >> ((9 - x) * 8)) & 0xff ));
                pixels.push(uint8( (tileColors.b[y] >> ((9 - x) * 8)) & 0xff ));
            }
        }
        return pixels;
    }
}
